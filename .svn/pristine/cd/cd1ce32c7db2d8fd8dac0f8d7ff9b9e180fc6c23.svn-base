#!/usr/bin/perl -w

# Sort dumped consensuses, statuses, descriptors etc into per-month folders.

# Copyright (c) 2006, 2007, 2008 Peter Palfrader
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in
# all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

use strict;
use File::Find;
use File::Basename;
use File::stat;
use Time::Local;


my $cutofftime;


sub wanted() {
	return unless -f;
	my $mtime = stat($_)->mtime;
	return if $mtime >= $cutofftime;

	my (undef,undef,undef,undef,$mon,$year,undef,undef,undef) = gmtime $mtime;

	my $bn = basename $_;
	my $dn = dirname $_;
	my @path = split /\//, $dn;
	$path[0] .= sprintf 's-%4d-%02d', 1900+$year, $mon+1;
	$dn = join '/', @path;

	if (! -d $dn) {
		my $p = '.';
		for my $component (@path) {
			$p .= '/'.$component;
			if (! -d $p) {
				mkdir $p or die ("Cannot mkdir $p: $!\n");
			};
		};
	};

	print "$_ -> $dn/$bn\n";
	rename $_, $dn.'/'.$bn or die ("Cannot rename $_ to $dn/$bn: $!\n");
};

my (undef,undef,undef,undef,$mon,$year,undef,undef,undef) = gmtime(time - 5*24*3600);
$cutofftime = timegm(0,0,0,1,$mon,$year);
find(	{
		wanted		=> \&wanted,
		no_chdir	=> 1
	},
	'server-descriptor');

find(	{
		wanted		=> \&wanted,
		no_chdir	=> 1
	},
	'extra-info');
